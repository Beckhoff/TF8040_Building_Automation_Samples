<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="FB_BA_ProjectEx" Id="{08d504d4-aa6b-425f-9d37-d4a0a90a0176}" SpecialFunc="None">
    <Declaration><![CDATA[{region '#Template'}
	{attribute 'Category' := 'General'}
	{attribute 'Version' := '0.0.1.0'}
	{attribute 'Tags' := 'General, Settings, Project, Basics'}
	{attribute 'Description' := 'Projektspezifische Informationen	// Project specific informations'}
{endregion}
FUNCTION_BLOCK FB_BA_ProjectEx EXTENDS FB_BA_Project
VAR_INPUT CONSTANT PERSISTENT
	{attribute 'parameterCategory' := 'Persistent Data'}
	tPrsData_DefaultPeriod : TIME := T#30M;
	{attribute 'parameterCategory' := 'Persistent Data'}
	tPrsData_ComissioningPeriod : TIME := T#5M;
	
	{attribute 'parameterCategory' := 'UPS'}
	eUPS : E_BA_UPSType := E_BA_UPSType.eNone;			  
END_VAR
VAR_INPUT CONSTANT
	{attribute 'parameterCategory' := 'Persistent Data'}
	bPrsData_ManualWriteData : BOOL;
END_VAR
VAR_OUTPUT
	nPrsDataWritten			: UINT;
	bPowerFail				: BOOL;
END_VAR
VAR
	_bTriggerUPS			: BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^();

UPS();
PersistentData();]]></ST>
    </Implementation>
    <Folder Name="[Protected] Events" Id="{18ef8c72-7d00-4368-8e98-1d93f55b58c2}" />
    <Folder Name="[Protected] Management" Id="{8520c257-67bf-4e78-9614-90801022e3c9}" />
    <Method Name="OnInitCycle" Id="{c47517d3-4aa0-48ad-8a4a-7a5f73ec9c98}" FolderPath="[Protected] Events\">
      <Declaration><![CDATA[METHOD PROTECTED OnInitCycle
VAR
	_stVersion		: ST_LibVersion;
END_VAR
VAR CONSTANT
	FORMAT			: STRING		:= '%d.%d.%d.%d';
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE (State) OF
	E_BA_ObjectState.eInstanceInit1:
		// Refresh runtime information:
		//_stVersion := F_GetVersion();
		_stVersion := stLibVersion_BA2_Templates;
		
		BA_Globals.Format(
			pFormatString := ADR(FORMAT),
			arg1:=F_UINT(_stVersion.iMajor),
			arg2:=F_UINT(_stVersion.iMinor),
			arg3:=F_UINT(_stVersion.iBuild),
			arg4:=F_UINT(_stVersion.iRevision),
		
			pDstString := ADR(stVersion.Project),
			nDstSize := SIZEOF(stVersion.Project)
		);
END_CASE

SUPER^();]]></ST>
      </Implementation>
    </Method>
    <Method Name="PersistentData" Id="{cad2e84a-aa7d-40e4-a2b8-4e4fd69da35c}" FolderPath="[Protected] Management\">
      <Declaration><![CDATA[METHOD PROTECTED PersistentData
VAR_INST
	nOnlineChangeCnt		:	UDINT;
	iStep					:	INT;
	fbTon					:	TON;
	fbResetOnlineChange		:	TON;
	rtigManual				:	R_TRIG;
	trigUPS					:	R_TRIG;
	rFinishTrig				:	R_TRIG;
	WrtPersistDat			:	FB_BA_WrtPersistDat;
END_VAR
VAR CONSTANT
	DETECT_COMISSIONING_DURATION : TIME := T#60M;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF	TwinCAT_SystemInfoVarList._AppInfo.OnlineChangeCnt <> nOnlineChangeCnt THEN
	nOnlineChangeCnt := TwinCAT_SystemInfoVarList._AppInfo.OnlineChangeCnt;
	fbResetOnlineChange(IN := FALSE);
	fbTon(IN := FALSE);
	iStep := 10;
END_IF

IF	tPrsData_DefaultPeriod < T#600S THEN
	tPrsData_DefaultPeriod	:= T#1800S;
END_IF

IF	tPrsData_ComissioningPeriod < T#30S THEN
	tPrsData_ComissioningPeriod	:= T#300S;
END_IF

CASE	iStep	OF
	0:
		fbTon(IN := NOT fbTon.Q,PT := tPrsData_DefaultPeriod);
	10:
		fbTon(IN := NOT fbTon.Q,PT := tPrsData_ComissioningPeriod);
		fbResetOnlineChange(IN := TRUE, PT := DETECT_COMISSIONING_DURATION);
		
		IF	fbResetOnlineChange.Q THEN
			iStep := 0;
			fbResetOnlineChange(IN := FALSE);
		END_IF
END_CASE
 
rtigManual(CLK := bPrsData_ManualWriteData);
bPrsData_ManualWriteData := FALSE;

trigUPS(CLK := _bTriggerUPS);

// Write persistent data:
WrtPersistDat(bStt:=(fbTon.Q OR rtigManual.Q OR trigUPS.Q));
rFinishTrig(CLK:=(WrtPersistDat.bBusy AND NOT WrtPersistDat.bErr));

IF (rFinishTrig.Q) THEN
	nPrsDataWritten := (nPrsDataWritten+1);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="UPS" Id="{76cde39e-2f67-48d1-8f10-1e016c5e204e}" FolderPath="[Protected] Management\">
      <Declaration><![CDATA[METHOD PROTECTED UPS
VAR_INST
	fbCX50x0			:	FB_S_UPS;
	fbCX51x0			:	FB_S_UPS_CX51x0;
	fbCX81xx			:	FB_S_UPS_CX81xx;
	fbCX9020_U900		:	FB_S_UPS_CX9020_U900;
	fbCB3011			:	FB_S_UPS_CB3011;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE eUPS OF
	E_BA_UPSType.eNone:
		;

	E_BA_UPSType.eCX50x0:
		fbCX50x0(
			sNetID:= '', 
		//	iPLCPort:= TwinCAT_SystemInfoVarList._AppInfo.AdsPort, 
			iUPSPort:= 16#4A8, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			eUpsMode:= eSUPS_CheckPowerStatus, 
			ePersistentMode:= SPDM_2PASS, 
			tRecoverTime:= T#10S, 
			bPowerFailDetect=> bPowerFail, 
			eState=> );

	E_BA_UPSType.eCX51x0:
		fbCX51x0(
			sNetID:= '', 
			iPLCPort:= TwinCAT_SystemInfoVarList._AppInfo.AdsPort, 
			iUPSPort:= 16#588, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			eUpsMode:= eSUPS_CheckPowerStatus, 
			ePersistentMode:= SPDM_2PASS, 
			tRecoverTime:= T#10S, 
			bPowerFailDetect=> bPowerFail, 
			eState=> );

	E_BA_UPSType.eCX81xx:
		fbCX81xx(
			sNetID:= '', 
			iPLCPort:= TwinCAT_SystemInfoVarList._AppInfo.AdsPort, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			eUpsMode:= eSUPS_CheckPowerStatus, 
			ePersistentMode:= SPDM_2PASS, 
			tRecoverTime:= T#10S, 
			bPowerFailDetect=> bPowerFail, 
			eState=> );
		
	E_BA_UPSType.eCX9020_U900:
		fbCX9020_U900(
			sNetID:= '', 
			iPLCPort:= TwinCAT_SystemInfoVarList._AppInfo.AdsPort, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			eUpsMode:= eSUPS_CheckPowerStatus, 
			ePersistentMode:= SPDM_2PASS, 
			tRecoverTime:= T#10S, 
			bPowerFailDetect=> bPowerFail, 
			eState=> );

	E_BA_UPSType.eCB3011:
		fbCB3011(
			sNetID:= '', 
			iPLCPort:= TwinCAT_SystemInfoVarList._AppInfo.AdsPort, 
			tTimeout:= DEFAULT_ADS_TIMEOUT, 
			eUpsMode:= eSUPS_CheckPowerStatus, 
			ePersistentMode:= SPDM_2PASS, 
			tRecoverTime:= T#10S, 
			bPowerFailDetect=> bPowerFail, 
			eState=> );
ELSE
	eUPS := E_BA_UPSType.eNone; 	
END_CASE]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_BA_ProjectEx">
      <LineId Id="48" Count="1" />
      <LineId Id="9" Count="0" />
      <LineId Id="26" Count="0" />
    </LineIds>
    <LineIds Name="FB_BA_ProjectEx.OnInitCycle">
      <LineId Id="68" Count="1" />
      <LineId Id="83" Count="1" />
      <LineId Id="89" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="90" Count="1" />
      <LineId Id="100" Count="3" />
      <LineId Id="93" Count="2" />
      <LineId Id="79" Count="2" />
      <LineId Id="41" Count="0" />
    </LineIds>
    <LineIds Name="FB_BA_ProjectEx.PersistentData">
      <LineId Id="6" Count="25" />
      <LineId Id="33" Count="3" />
      <LineId Id="52" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="60" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="64" Count="1" />
      <LineId Id="62" Count="0" />
      <LineId Id="66" Count="1" />
    </LineIds>
    <LineIds Name="FB_BA_ProjectEx.UPS">
      <LineId Id="23" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="39" Count="8" />
      <LineId Id="25" Count="2" />
      <LineId Id="49" Count="8" />
      <LineId Id="28" Count="0" />
      <LineId Id="6" Count="1" />
      <LineId Id="59" Count="7" />
      <LineId Id="8" Count="2" />
      <LineId Id="68" Count="7" />
      <LineId Id="11" Count="0" />
      <LineId Id="18" Count="1" />
      <LineId Id="30" Count="7" />
      <LineId Id="20" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>