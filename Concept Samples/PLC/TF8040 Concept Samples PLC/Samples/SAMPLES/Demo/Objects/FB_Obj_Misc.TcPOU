<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_Obj_Misc" Id="{69b1c2ea-7343-455f-ae88-cfe6d2858d6e}" SpecialFunc="None">
    <Declaration><![CDATA[// Demonstration of objects.
// > See "FB_BA_EC01" to view decleared event classes.
FUNCTION_BLOCK FB_Obj_Misc EXTENDS FB_BA_View
VAR CONSTANT
	TREND_COUNT									: INT										:= 10;
END_VAR
VAR
	{region 'Project'}
		Proj									: FB_BA_View := (
													iParent									:= THIS^,
													sDescription							:= '{Project information}'
												);
				 
			// Intention:						Project Information
			Project								: FB_BA_Project := (
													iParent									:= Proj,
													sDescription							:= '{My project infos}',
													stProject := (	
														sTitle								:= 'Mein Projekt',
														sDescription						:= 'PLC Beispielprojekt',
														sLocation							:= 'Verl'
													)
												);
	{endregion}
	{region 'Loop'}
		Loop									: FB_BA_View := (
													iParent									:= THIS^,
													sDescription							:= '{Loop objects}'
												);
				 
			fLoop_Setpoint						: REAL										:= 20;								// Temperature setpoint.
			fLoop_CtrlVal						: REAL										:= 16;								// Simulation of controlled variable value.

			// Initialization:					Single loop for heating.
			Heat_En								: REFERENCE TO BOOL							REF= Heat.bEn;
			Heat_EnSync							: REFERENCE TO BOOL							REF= Heat.bEnSync;
			Heat_ValSync						: REFERENCE TO REAL							REF= Heat.fValSync;
			Heat_Y								: REFERENCE TO REAL							REF= Heat.fPresentValue;
			Heat								: FB_BA_LoopSeq := (
													iParent									:= Loop,
													sDescription							:= '{Heating Loop}',
													eOutputUnit								:= E_BA_Unit.eOther_Percent,
													bEn										:= TRUE,
													eActionRm								:= E_BA_Action.eReverse,
													fProportionalConstant					:= 3.0,
													fIntegralConstant						:= 180
												);

			// Initialization:					Single loop for heating.
			Cool_En								: REFERENCE TO BOOL							REF= Cool.bEn;
			Cool_EnSync							: REFERENCE TO BOOL							REF= Cool.bEnSync;
			Cool_ValSync						: REFERENCE TO REAL							REF= Cool.fValSync;
			Cool_Y								: REFERENCE TO REAL							REF= Cool.fPresentValue;
			Cool								: FB_BA_LoopSeq := (
													iParent									:= Loop,
													sDescription							:= '{Cooling Loop}',
													eOutputUnit								:= E_BA_Unit.eOther_Percent,
													bEn										:= TRUE,
													eActionRm								:= E_BA_Action.eDirect,
													fProportionalConstant					:= 3.0,
													fIntegralConstant						:= 180
												);
	{endregion}
	{region 'Calendar'}
		Cals									: FB_BA_View := (
													iParent									:= THIS^,
													sDescription							:= '{Calendar objects}'
												);

			// Intention:						Initialize calendar parameters with parameter builders.
			// Parameter:						tWeek										= F_BA_CalendarBuilder()
			Cal1								: FB_BA_Cal := (
													iParent									:= Cals,
													sDescription							:= '{Calendar 1}',
													aDateList								:= F_BA_CalendarBuilder()
																								.AppendDate(2021, E_BA_Month.eMarch, 10)
																								.AppendDateRange(nFromYear := 2021,E_BA_Month.eMarch,10, nToYear := 2021,E_BA_Month.eMarch,15)
																								.AppendWeekNDay(E_BA_Weekday.eMonday, E_BA_Week.Unspecified, E_BA_Month.Unspecified)
																								.Build()
												);
													 
			// Intention:						Initialize calendar parameters with helper methods.
			//									aDateList									= [(..., F_BA_DateVal(...)), (..., F_BA_DateRangeVal(...)), (..., F_BA_WeekNDayVal(...))]
			Cal2								: FB_BA_Cal := (
													iParent									:= Cals,
													sDescription							:= '{Calendar 2}',
													aDateList := [(
														eType								:= E_BA_DateValChoice.eDate,
														uDate								:= F_BA_DateVal(2020,E_BA_Month.eDecember,10)
													),(
														eType								:= E_BA_DateValChoice.eDateRange,
														uDate								:= F_BA_DateRangeVal(nFromYear:=2020,E_BA_Month.eMarch,5, nToYear:=2020,E_BA_Month.eMarch,10)
													),(
														eType								:= E_BA_DateValChoice.eWeekNDay,
														uDate								:= F_BA_WeekNDayVal(E_BA_Weekday.eTuesday, E_BA_Week.Unspecified, E_BA_Month.Unspecified)
													)]
												);

			Cal3								: FB_BA_Cal := (
													iParent									:= Cals,
													sDescription							:= '{Calendar 3}'
												);
	{endregion}
	{region 'Scheduler'}
		Sched									: FB_BA_View := (
													iParent									:= THIS^,
													sDescription							:= '{Schedule objects}'
												);
			
			// Intention:						Initialize scheduler parameters with parameter builders.
			// Parameter:						tWeek										= F_BA_WeeklyScheduleBuilder()
			//									tCalendar									= F_BA_ScheduleCalendarBuilder()
			//									tException									= F_BA_ExceptionScheduleBuilder()
			SchedA								: FB_BA_SchedA := (
													iParent									:= Sched,
													sDescription							:= '{Schedule Analog}',
													eUnit									:= E_BA_Unit.eTemperature_DegreesCelsius,
													aWeek									:= F_BA_WeeklyScheduleBuilder()
																								.SetAllDays()
																									.AppendA(T#0H, 1.0)
																									.Close()
																								.SetDay(E_BA_Weekday.eMonday)
																									.AppendA(T#0H, 2.0).AppendA(T#8H, 3.0).AppendA(T#16H, 2.0)
																									.Close()
																								.SetDayRange(E_BA_Weekday.eSaturday, E_BA_Weekday.eSunday)
																									.AppendA(T#0H, 4.0)
																									.Close()
																								.Build(),
													aCalendar								:= F_BA_ScheduleCalendarBuilder()
																								.AppendReference(Cal1)
																									.AppendA(T#0H, 1.0).AppendA(T#6H, 2.0).AppendNull(T#12H)
																									.Close()
																								.AppendReference(Cal2)
																									.AppendA(T#10H, 1.0).AppendNull(T#12H)
																									.Close()
																								.Build(),
													aException								:= F_BA_ExceptionScheduleBuilder()
																								.AppendDate(2021, E_BA_Month.eMarch, 10)
																									.AppendA(T#0H, 1.0).AppendA(T#6H, 2.0).AppendNull(T#12H)
																									.Close()
																								.AppendDateRange(nFromYear := 2021,E_BA_Month.eMarch,10, nToYear := 2021,E_BA_Month.eMarch,15)
																									.AppendA(T#12H, 1.0).AppendNull(T#16H)
																									.Close()
																								.AppendWeekNDay(E_BA_Weekday.eFriday, E_BA_Week.Unspecified, E_BA_Month.Unspecified)
																									.AppendA(T#20H, 3.0)
																									.Close()
																								.Build()
												);
			
			// Intention:						Initialize scheduler parameters with helper methods.
			// Parameter:						tWeek										= F_BA_SchedWeeklyXxA(...)
			//									tCalendar									= F_BA_SchedCalendar(...)
			//									tException									= [(..., F_BA_SchedEntryXxA(...)), (..., F_BA_SchedEntryXxA(...)), (..., F_BA_SchedEntryXxA(...))]
			SchedB								: FB_BA_SchedB := (
													iParent									:= Sched,
													sDescription							:= '{Schedule Bool}',
													sInactiveText							:= 'Aus',
													sActiveText								:= 'Ein',
													aWeek									:= F_BA_SchedWeekly3xB(E_BA_Weekday.eMonday, E_BA_Weekday.eFriday, T#0H, FALSE, T#6H, TRUE, T#20H, FALSE),
													aCalendar := [(
														iRefCalendar						:= Cal1,
														aEntry								:= F_BA_SchedEntry2xB(T#8H, TRUE, T#18H, FALSE)
													)],
													aException := [(
														eType								:= E_BA_DateValChoice.eDate,
														uDate								:= F_BA_DateVal(2021,E_BA_Month.eMarch,10),
														aEntry								:= F_BA_SchedEntry1xB(T#0H, FALSE)
													),(
														eType								:= E_BA_DateValChoice.eDateRange,
														uDate								:= F_BA_DateRangeVal(nFromYear:=2021,E_BA_Month.eMarch,10, nToYear:=2021,E_BA_Month.eMarch,15),
														aEntry								:= F_BA_SchedEntry2xB(T#10H, TRUE, T#17H, F_BA_NullB())
													),(
														eType								:= E_BA_DateValChoice.eWeekNDay,
														uDate								:= F_BA_WeekNDayVal(E_BA_Weekday.eFriday, E_BA_Week.Unspecified, E_BA_Month.Unspecified),
														aEntry								:= F_BA_SchedEntry3xB(T#9H, FALSE, T#12H, TRUE, T#17H, F_BA_NullB())
													)]);
			// Intention:						Initialize scheduler parameters manually.
			//									tCalendar									= ...
			//									tException									= ...
			SchedM								: FB_BA_SchedM := (
													iParent									:= Sched,
													sDescription							:= '{Schedule Multistate}',
													aStateText								:= [ 'Aus', 'Stufe 1', 'Stufe 2', 'Stufe 3' ],
													aWeek									:= F_BA_SchedWeekly3xM(E_BA_Weekday.eMonday, E_BA_Weekday.eFriday, T#0H, 1, T#6H, 2, T#20H, 1),
													aCalendar := [(
														iRefCalendar						:= Cal1,
														aEntry := [(
															eState							:= E_BA_SchedEntryState.eValue,
															stTime							:= F_BA_ToSTTime(T#16H),
															uValue							:= F_BA_UDIVal(2)
														),(
															eState							:= E_BA_SchedEntryState.eValue,
															stTime							:= F_BA_ToSTTime(T#20H),
															uValue							:= F_BA_UDIVal(1)
														)]
													)],
													aException := [(
														eType								:= E_BA_DateValChoice.eWeekNDay,
														uDate								:= F_BA_WeekNDayVal(E_BA_Weekday.eFriday, E_BA_Week.Unspecified, E_BA_Month.Unspecified),
														aEntry := [(
															eState							:= E_BA_SchedEntryState.eValue,
															stTime							:= F_BA_ToSTTime(T#10H),
															uValue							:= F_BA_UDIVal(2)
														),(
															eState							:= E_BA_SchedEntryState.eValue,
															stTime							:= F_BA_ToSTTime(T#12H),
															uValue							:= F_BA_UDIVal(3)
														),(
															eState							:= E_BA_SchedEntryState.eNull,
															stTime							:= F_BA_ToSTTime(T#16H)
														)]
													)]);
	{endregion}
	{region 'Trend'}
		Trnd									: FB_BA_View := (
													iParent									:= THIS^,
													sDescription							:= '{Trend objects}'
												);
				 
			// Intention:						Trend in polling-mode
			// Parameter:						eLoggingType								= E_BA_LoggingType.ePolled
			PollAV								: FB_BA_AV_Op := (
													iParent									:= Trnd,
													sDescription							:= '{Referenced object for polling}',
													eUnit									:= E_BA_Unit.eTemperature_DegreesCelsius,
													fValueRm								:= 3
												);
			TrendPoll							: FB_BA_Trend := (
													iParent									:= Trnd,
													sDescription							:= '{Sample Trend}',
													nEventClassID							:= 11,
													bEnable									:= TRUE,
													stStartTime								:= F_BA_ToSTDateTime(DT#2010-01-01-00:00),
													eLoggingType							:= E_BA_LoggingType.ePolled,
													nLogInterval							:= 300,
													stReferencedParam							:= F_BA_Reference(PollAV, E_BA_Parameter.ePresentValue)
												);
													
			// Intention:						Trend in trigger-mode
			// Parameter:						eLoggingType								= E_BA_LoggingType.ePolled
			TrigAV								: FB_BA_AV_Op := (
													iParent									:= Trnd,
													sDescription							:= '{Referenced object for polling}',
													eUnit									:= E_BA_Unit.eTemperature_DegreesCelsius,
													fValueRm								:= 1
												);
			TrendTrig_Man						: BOOL;
			TrendTrig							: FB_BA_Trend := (
													iParent									:= Trnd,
													sDescription							:= '{Sample Trend}',
													nEventClassID							:= 11,
													bEnable									:= TRUE,
													stStartTime								:= F_BA_ToSTDateTime(DT#2010-01-01-00:00),
													eLoggingType							:= E_BA_LoggingType.eTriggered,
													stReferencedParam						:= F_BA_Reference(TrigAV, E_BA_Parameter.ePresentValue)
												);
													 
			// Intention:						Trend in COV-mode
			// Parameter:						eLoggingType								= E_BA_LoggingType.eCOV
			CovAI								: FB_BA_AI := (
													iParent									:= Trnd,
													sDescription							:= '{Referenced analog object for COV}',
													eUnit									:= E_BA_Unit.eOther_Percent,
													nEventClassID							:= 11,
													bEventDetectionEnable					:= FALSE
												);
			TrendCovAI							: FB_BA_Trend := (
													iParent									:= Trnd,
													sDescription							:= '{Sample Analog Trend COV}',
													nEventClassID							:= 11,
													bEnable									:= TRUE,
													nBufferSize								:= 100,
													eLoggingType							:= E_BA_LoggingType.eCOV,
													stReferencedParam							:= F_BA_Reference(CovAI, E_BA_Parameter.ePresentValue)
												);
			CovBI								: FB_BA_BI := (
													iParent									:= Trnd,
													sDescription							:= '{Referenced binary object for COV}',
													sInactiveText							:= 'Off',
													sActiveText								:= 'On',
													nEventClassID							:= 11,
													bEventDetectionEnable					:= FALSE
												);
			TrendCovBI							: FB_BA_Trend := (
													iParent									:= Trnd,
													sDescription							:= '{Sample Binary Trend COV}',
													nEventClassID							:= 11,
													bEnable									:= TRUE,
													nBufferSize								:= 100,
													eLoggingType							:= E_BA_LoggingType.eCOV,
													stReferencedParam							:= F_BA_Reference(CovBI, E_BA_Parameter.ePresentValue)
												);
			CovMI								: FB_BA_MI := (
													iParent									:= Trnd,
													sDescription							:= '{Referenced multistate object for COV}',
													aStateText								:= ['Eco', 'Standard', 'Comfort'],
													nEventClassID							:= 11,
													bEventDetectionEnable					:= FALSE
												);
			TrendCovMI							: FB_BA_Trend := (
													iParent									:= Trnd,
													sDescription							:= '{Sample Multistate Trend COV}',
													nEventClassID							:= 11,
													bEnable									:= TRUE,
													nBufferSize								:= 100,
													eLoggingType							:= E_BA_LoggingType.eCOV,
													stReferencedParam							:= F_BA_Reference(CovMI, E_BA_Parameter.ePresentValue)
												);
													 
			// Intention:						Multiple trend objects as array
			Trends								: FB_BA_View := (
													iParent									:= Trnd,
													sDescription							:= 'Multiple trend objects'
												);
				Trend							: ARRAY[1 .. TREND_COUNT] OF FB_BA_Trend := [TREND_COUNT((
													iParent									:= Trends,
													iLabel									:= MyLabels.Trnd,
													nEventClassID							:= 11,
													eLoggingType							:= E_BA_LoggingType.eCOV
												))];
	{endregion}
END_VAR
VAR			
	_i											: INT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^();

// Project:
Proj();

Project();

// Calendar:
Cals();

Cal1();
Cal2();
Cal3();

// Scheduler:
Sched();

SchedA();
SchedB();
SchedM();

// Loop:
Loop();

Heat(
	fSetpoint := fLoop_Setpoint,
	fCtrlVal := fLoop_CtrlVal
);
Cool(
	fSetpoint := fLoop_Setpoint,
	fCtrlVal := fLoop_CtrlVal
);

// Trend:
Trnd();

PollAV();
TrendPoll();
TrigAV(fValuePgm:=(TrigAV.fValuePgm+0.01));
TrendTrig(bTrigPgm:=((MAIN.nCounter MOD 250 = 0) OR (TrendTrig_Man)));
IF (MAIN.nCounter MOD 200 = 0) THEN
	CovAI.fVal := SEL((CovAI.fVal<10), 0, (CovAI.fVal+1));
END_IF
IF (MAIN.nCounter MOD 500 = 0) THEN
	CovBI.bVal := (NOT CovBI.bVal);
	CovMI.nVal := SEL((CovMI.nVal<CovMI.StateCount), 1, (CovMI.nVal+1));
END_IF
CovAI();
TrendCovAI();
CovBI();
TrendCovBI();
CovMI();
TrendCovMI();
Trends();
FOR _i := 1 TO TREND_COUNT DO
	Trend[_i]();
END_FOR
]]></ST>
    </Implementation>
    <LineIds Name="FB_Obj_Misc">
      <LineId Id="9" Count="0" />
      <LineId Id="319" Count="0" />
      <LineId Id="710" Count="0" />
      <LineId Id="759" Count="0" />
      <LineId Id="711" Count="1" />
      <LineId Id="811" Count="0" />
      <LineId Id="810" Count="0" />
      <LineId Id="809" Count="0" />
      <LineId Id="812" Count="0" />
      <LineId Id="814" Count="5" />
      <LineId Id="874" Count="0" />
      <LineId Id="872" Count="0" />
      <LineId Id="875" Count="1" />
      <LineId Id="813" Count="0" />
      <LineId Id="1806" Count="0" />
      <LineId Id="757" Count="1" />
      <LineId Id="713" Count="2" />
      <LineId Id="720" Count="0" />
      <LineId Id="725" Count="2" />
      <LineId Id="1430" Count="0" />
      <LineId Id="2149" Count="0" />
      <LineId Id="881" Count="0" />
      <LineId Id="888" Count="0" />
      <LineId Id="948" Count="0" />
      <LineId Id="928" Count="0" />
      <LineId Id="1040" Count="0" />
      <LineId Id="2494" Count="1" />
      <LineId Id="1039" Count="0" />
      <LineId Id="1037" Count="0" />
      <LineId Id="1047" Count="3" />
      <LineId Id="1028" Count="5" />
      <LineId Id="929" Count="0" />
      <LineId Id="931" Count="2" />
      <LineId Id="753" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>